model {


    ###################################
    ## intercept, sex,
    ##################################
    ## i=1 for direct effects, i=2 for effects on mediator
    for(i in 1:2)
    {
	intercept[i] ~ dnorm(0, .0000001)
	f.sex[i]     ~ dnorm(0, .0000001)
    }


    ###################################
    ## f.cross 
    ###################################
    precision.cross <- .0000001
    for(i in 1:2)
    {
        f.cross[i,1]  <- 0
    	for(j in 2:n.cross)
    	{
             f.cross[i,j] ~ dnorm(0, precision.cross)
        }
    }


    ###########################################
    ## f.tissue  (consider making random effect)
    ###########################################
    precision.tissue <- .0000001
    for(i in 1:2)
    {
        f.tissue[i,1]  <- 0
    	for(j in 2:n.tissue)
    	{
             f.tissue[i,j] ~ dnorm(0, precision.tissue)
        }
    }


    ###################################
    ## f.sex-by-f.cross-effect
    ##################################
    precision.sex.cross <- .0000001
    for(i in 1:2)
    {
        f.sex.cross[i,1]  <- 0
    	for(j in 2:n.cross)
    	{
             f.sex.cross[i,j] ~ dnorm(0, precision.sex.cross)
        }
    }


    ###################################
    ## f.sex-by-f.tissue-effect (consider making random effect)
    ##################################
    precision.sex.tissue <- .0000001
    for(i in 1:2)
    {
        f.sex.tissue[i,1]  <- 0
    	for(j in 2:n.tissue)
    	{
             f.sex.tissue[i,j] ~ dnorm(0, precision.sex.tissue)
        }
    }


    ###################################
    ## f.cross-by-f.tissue-effect (consider making random effect)
    ##################################
    precision.cross.tissue <- .0000001
    for(i in 1:2)
    {
        f.cross.tissue[i,1]  <- 0
    	for(j in 2:n.cross.tissue)
    	{
             f.cross.tissue[i,j] ~ dnorm(0, precision.cross.tissue)
        }
    }

    ###################################
    ## f.cross-by-f.direction-effect 
    ##################################
    precision.cross.direction <- .0000001
    for(i in 1:2)
    {
        f.cross.direction[i,1]  <- 0	
    	for(j in 2:n.cross)
    	{
             f.cross.direction[i,j] ~ dnorm(0, precision.cross.direction)
        }
    }

    ###################################
    ## f.sex-by-f.cross-by-f.tissue-effect (consider making random effect)
    ##################################
    precision.sex.cross.tissue <- .0000001
    for(i in 1:2)
    {
        f.sex.cross.tissue[i,1]  <- 0
    	for(j in 2:n.cross.tissue)
    	{
             f.sex.cross.tissue[i,j] ~ dnorm(0, precision.sex.cross.tissue)
        }
    }

    
    ###################################
    ## f.sex-by-f.cross-by-f.direction-effect (consider making random effect)
    ##################################
    precision.sex.cross.direction <- .0000001
    for(i in 1:2)
    {
        f.sex.cross.direction[i,1]  <- 0
    	for(j in 2:n.cross)
    	{
             f.sex.cross.direction[i,j] ~ dnorm(0, precision.sex.cross.direction)
        }
    }


    ###################################
    ## f.cross-by-f.tissue-by-f.direction-effect (consider making random effect)
    ##################################
    precision.cross.tissue.direction <- .0000001
    for(i in 1:2)
    {
        f.cross.tissue.direction[i,1]  <- 0
    	for(j in 2:n.cross.tissue)
    	{
             f.cross.tissue.direction[i,j] ~ dnorm(0, precision.cross.tissue.direction)
        }
    }

    ###################################
    ## f.sex-by-f.cross-by-f.tissue-by-f.direction-effect (consider making random effect)
    ##################################
    precision.sex.cross.tissue.direction <- .0000001
    for(i in 1:2)
    {
        f.sex.cross.tissue.direction[i,1]  <- 0
    	for(j in 2:n.cross.tissue)
    	{
             f.sex.cross.tissue.direction[i,j] ~ dnorm(0, precision.sex.cross.tissue.direction)
        }
    }



    ###################################
    ## mouse effect
    ##################################
    for(i in 1:2)
    {
        std.mouse[i] ~ dunif(0,1000)
        precision.mouse[i] <- pow(std.mouse[i],-2)
        for(j in 1:n.mouse)
    	{
    	     ranef.mouse[i,j] ~ dnorm(0, precision.mouse[i])
    	}
    }


    ############################################################
    ## Beta- the effect of the mediator m on y.
    ## Assumed to be independent of all other covariates.
    ############################################################
    beta ~ dnorm(0, .0000001)
    ##beta <- 0

    ############################
    ## Sigmas for y and mediator.
    ############################
    for(i in 1:2)
    {
        std.epsilon[i] ~ dunif(0,1000)
        precision.epsilon[i] <- pow(std.epsilon[i],-2)
    }


    #########################################
    # Likelihood functions, indexed by s, the sample
    ###############################################
    for(s in 1:n)
    {
       ##compute the means first
       for(i in 1:2)
       {
       u[i,s] <- intercept[i]                                       +
                 f.sex[i]*sex[s]                                    +
		 f.cross[i,                     cross.j[s]]         +	
		 f.tissue[i,                   tissue.j[s]]         +

		 f.sex.cross[i,                  cross.j[s]]*sex[s]  +
		 f.sex.tissue[i,                tissue.j[s]]*sex[s]  +
		 f.cross.tissue[i,               cross.tissue[s]] +
		 f.cross.direction[i,            cross.j[s]]*direction[s] +
		 
		 f.sex.cross.tissue[i,           cross.tissue[s]]*sex[s] +
		 f.sex.cross.direction[i,        cross.j[s]]*sex[s]*direction[s] +
		 f.cross.tissue.direction[i,     cross.tissue[s]]*direction[s] +
		 f.sex.cross.tissue.direction[i, cross.tissue[s]]*sex[s]*direction[s] +

		 ranef.mouse[i,     mouse.j[s]]
       }

       ##The mediator likelihood.
       mediator[s] ~ dnorm(u[2,s], precision.epsilon[2])
       ##Data likelihood
       y[s] ~ dnorm(u[1,s] + beta*mediator[s], precision.epsilon[1])

##       epsilon[1,s] <- y[s] - u[1,s]
    }
}
